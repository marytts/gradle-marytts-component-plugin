plugins {
    id 'de.dfki.mary.component'
}

def componentName = 'Hello'
def componentPackageName = 'world'

marytts {
    component {
        name = componentName
        packageName = componentPackageName
    }
}

task testPlugins {
    doLast {
        assert pluginManager.hasPlugin('de.dfki.mary.component')
        assert pluginManager.hasPlugin('groovy')
    }
}

task testExtension {
    doLast {
        assert marytts.component.name == componentName
        assert marytts.component.packageName == componentPackageName
    }
}

task testGenerateSource {
    dependsOn generateSource
    doLast {
        assert sourceSets.main.allSource.contains(file("$buildDir/generatedSrc/main/groovy/$componentPackageName/${componentName}Config.groovy"))
        assert sourceSets.test.allSource.contains(file("$buildDir/generatedSrc/test/groovy/$componentPackageName/${componentName}ConfigTest.groovy"))
    }
}

task testGenerateConfig {
    dependsOn generateConfig
    doLast {
        assert file("$buildDir/hello.config").exists()
    }
}

task testProcessResources {
    dependsOn processResources
    doLast {
        assert fileTree(sourceSets.main.output.resourcesDir).contains(file("$buildDir/resources/main/$componentPackageName/hello.config"))
    }
}

task testCompileGroovy {
    dependsOn compileGroovy
    doLast {
        assert fileTree(sourceSets.main.groovy.outputDir).contains(file("$buildDir/classes/groovy/main/$componentPackageName/${componentName}Config.class"))
    }
}

task testCompileTestGroovy {
    dependsOn compileTestGroovy
    doLast {
        assert fileTree(sourceSets.test.groovy.outputDir).contains(file("$buildDir/classes/groovy/test/$componentPackageName/${componentName}ConfigTest.class"))
    }
}
