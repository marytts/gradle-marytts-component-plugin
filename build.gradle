plugins {
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'groovy'
    id 'jacoco'
    id 'signing'
}

group 'de.dfki.mary'
version '0.4.0-SNAPSHOT'
description 'Utility plugin to build MaryTTS components with Gradle'
ext.isReleaseVersion = !version.endsWith('-SNAPSHOT')

pluginBundle {
    def url = 'https://github.com/marytts/gradle-marytts-component-plugin'
    website = url
    vcsUrl = url
    description = project.description
    tags = ['marytts', 'text-to-speech', 'tts', 'speech synthesis']
}

gradlePlugin {
    plugins {
        componentPlugin {
            id = 'de.dfki.mary.component'
            implementationClass = 'de.dfki.mary.ComponentPlugin'
            displayName = 'Gradle MaryTTS Component Plugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api group: 'org.yaml', name: 'snakeyaml', version: '2.2'
    testImplementation group: 'org.testng', name: 'testng', version: '7.5.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

test {
    useTestNG()
    systemProperty 'java.io.tmpdir', temporaryDir
    failFast = true
    testLogging {
        events 'passed', 'failed', 'skipped'
        exceptionFormat = 'full'
        info {
            showStandardStreams = true
        }
    }
    finalizedBy jacocoTestReport
    jacoco {
        excludes = [
                'de.dfki.mary.ComponentPlugin',
                'de.dfki.mary.tasks.*'
        ]
    }
}

jacocoTestReport {
    dependsOn test
    reports.xml.required = true
}

publishing {
    publications {
        withType(MavenPublication).configureEach {
            pom {
                name = 'MaryTTS Component Plugin'
                description = project.description
                url = 'https://github.com/marytts/gradle-marytts-component-plugin'
                inceptionYear = '2017'
                organization {
                    name = 'DFKI GmbH'
                    url = 'https://dfki.de/'
                }
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0-standalone.html'
                    }
                }
                developers {
                    developer {
                        name = 'SÃ©bastien Le Maguer'
                        url = 'https://github.com/seblemaguer'
                    }
                    developer {
                        name = 'Ingmar Steiner'
                        url = 'https://github.com/psibre'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/marytts/gradle-marytts-component-plugin.git'
                    developerConnection = 'scm:git:git@github.com/marytts/gradle-marytts-component-plugin.git'
                    url = 'https://github.com/marytts/gradle-marytts-component-plugin'
                }
            }
        }
    }
    repositories {
        maven {
            name 'OSSRH'
            url isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    : 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}
